apply from: "${rootDir}/gradle/mwe2-workflows.gradle"

dependencies {
	compile project(':org.eclipse.xtext.ide')
	compile project(':org.eclipse.xtext.testing')
	compile project(':org.eclipse.xtext.testlanguages')
	compile project(':org.eclipse.xtext.testlanguages.ide')
	compile 'junit:junit'
	compile 'org.eclipse.lsp4j:org.eclipse.lsp4j'
	compile 'org.eclipse.emf:org.eclipse.emf.ecore.change'
}

sourceSets.test.java {
	srcDir 'testlang-src'
	srcDir 'testlang-src-gen'
	srcDir 'suites'
}

sourceSets.test.resources {
	srcDir 'testlang-src'
	srcDir 'testlang-src-gen'
}

test {   
   exclude '**/*Suite.class'
}

task generateTestLanguages(type: XtextGeneratorTask) {
	workflow = file('testlang-src/org/eclipse/xtext/ide/tests/testlanguage/GenerateTestLanguage.mwe2')
	inputs.file 'testlang-src/org/eclipse/xtext/ide/tests/testlanguage/TestLanguage.xtext'
	outputs.dir 'testlang-src-gen'
	dependsOn 'processTestResources'
}

tasks.getByName('compileTestJava') {
	mustRunAfter 'generateTestLanguages'
}

tasks.getByName('compileJava') {
	mustRunAfter 'generateTestLanguages'
}

if (tasks.findByName('generateTestXtext')) {
	tasks.getByName('generateTestXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}

if (tasks.findByName('generateXtext')) {
	tasks.getByName('generateXtext') {
		mustRunAfter 'generateTestLanguages'
	}
}
